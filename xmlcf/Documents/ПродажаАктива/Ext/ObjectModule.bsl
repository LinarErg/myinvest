
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Отказ = Ложь;	
	
	ПроверитьВозможностьПроведения(Отказ);	
	
	Если НЕ Отказ тогда
		ВыполнитьДвиженияПоРегистрам();
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПроверитьВозможностьПроведения(Отказ)
	
	Движения.ОстаткиДенежныхСредств.Очистить();
	Движения.ОстаткиДенежныхСредств.Записывать = Истина;
	Движения.Записать();
	
	Движения.АктивыОстатки.Очистить();
	Движения.АктивыОстатки.Записывать = Истина;
	Движения.Записать();

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивыОстаткиОстатки.Актив КАК Актив,
		|	АктивыОстаткиОстатки.Валюта КАК Валюта,
		|	АктивыОстаткиОстатки.Портфель КАК Портфель,
		|	АктивыОстаткиОстатки.СуммаОстаток КАК СуммаОстаток,
		|	АктивыОстаткиОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.АктивыОстатки.Остатки(
		|			&Период,
		|			Портфель = &Портфель
		|				И Валюта = &Валюта
		|				И Актив = &Актив) КАК АктивыОстаткиОстатки";
	
	Запрос.УстановитьПараметр("Валюта", Актив.Валюта);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Портфель", Портфель);
	Запрос.УстановитьПараметр("Актив", Актив);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоОстаток < Количество тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;	
		
КонецПроцедуры

Процедура ВыполнитьДвиженияПоРегистрам()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаАктива.Актив КАК Актив,
		|	ПродажаАктива.Сумма КАК СуммаВДокументе,
		|	ПродажаАктива.Количество КАК КоличествоВДокументе,
		|	АктивыОстаткиОстатки.ДокументПартии КАК ДокументПартии,
		|	ЕСТЬNULL(АктивыОстаткиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(АктивыОстаткиОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
		|ИЗ
		|	Документ.ПродажаАктива КАК ПродажаАктива
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.АктивыОстатки.Остатки(
		|				&Период,
		|				Портфель = &Портфель
		|					И Актив = &Актив
		|					И Валюта = &Валюта) КАК АктивыОстаткиОстатки
		|		ПО ПродажаАктива.Портфель = АктивыОстаткиОстатки.Портфель
		|			И ПродажаАктива.Актив = АктивыОстаткиОстатки.Актив
		|			И ПродажаАктива.ВалютаСделки = АктивыОстаткиОстатки.Валюта
		|ГДЕ
		|	ПродажаАктива.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	АктивыОстаткиОстатки.ДокументПартии.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(СуммаВДокументе),
		|	МАКСИМУМ(КоличествоВДокументе),
		|	СУММА(СуммаОстаток),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Актив";
	
	Запрос.УстановитьПараметр("Актив", Актив);
	Запрос.УстановитьПараметр("Валюта", ВалютаСделки);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Портфель", Портфель);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаАктив = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаАктив.Следующий() Цикл		
		
		ОсталосьСписать = ВыборкаАктив.КоличествоВДокументе;	
		
		ВыборкаДетальныеЗаписи = ВыборкаАктив.Выбрать();	
		Пока ВыборкаДетальныеЗаписи.Следующий() И ОсталосьСписать <> 0 Цикл
			КСписанию = Мин(ОсталосьСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);			
			СуммаСписания = ?(ВыборкаДетальныеЗаписи.КоличествоОстаток = КСписанию,	ВыборкаДетальныеЗаписи.СуммаОстаток, ВыборкаДетальныеЗаписи.СуммаОстаток/ВыборкаДетальныеЗаписи.КоличествоОстаток * КСписанию);
			
			Движения.АктивыОстатки.Записывать = Истина;
			Движение = Движения.АктивыОстатки.Добавить();
			Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
			Движение.Период 		= Дата;
			Движение.Портфель 		= Портфель;
			Движение.Актив 			= Актив;
			Движение.ДокументПартии = ВыборкаДетальныеЗаписи.ДокументПартии;			
			Движение.Валюта 		= ВалютаСделки;
			Движение.Сумма 			= Окр(СуммаСписания,5);
			Движение.Количество 	= КСписанию;
			
			ОсталосьСписать = ОсталосьСписать - КСписанию;
		КонецЦикла;
	КонецЦикла;
	
	Движения.ОстаткиДенежныхСредств.Записывать = Истина;
	Движение = Движения.ОстаткиДенежныхСредств.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;;
	Движение.Период 	 = Дата;
	Движение.ВидОперации = Перечисления.ВидыОперацийПриходРасходДенежныхСредств.ПродажаАктива;
	Движение.Портфель 	 = Портфель;
	Движение.Валюта 	 = ВалютаСделки;
	Движение.Сумма 		 = Сумма;
	
	Если СуммаКомиссии <> 0 тогда
		Движения.ОстаткиДенежныхСредств.Записывать = Истина;
		Движение = Движения.ОстаткиДенежныхСредств.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период 	 = Дата;
		Движение.ВидОперации = Перечисления.ВидыОперацийПриходРасходДенежныхСредств.КомиссияБрокера;		
		Движение.Портфель 	 = Портфель;		
		Движение.Валюта 	 = ВалютаСделки;
		Движение.Сумма 		 = СуммаКомиссии;
	КонецЕсли;
	
	Если НКД <> 0 тогда
		Движения.ОстаткиДенежныхСредств.Записывать = Истина;
		Движение = Движения.ОстаткиДенежныхСредств.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период 	 = Дата;
		Движение.ВидОперации = Перечисления.ВидыОперацийПриходРасходДенежныхСредств.НКД;		
		Движение.Портфель 	 = Портфель;		
		Движение.Валюта 	 = ВалютаСделки;
		Движение.Сумма 		 = НКД;
	КонецЕсли;
	
КонецПроцедуры



