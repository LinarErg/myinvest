
#Область Вспомогательные

Функция ПолучитьСерверИсточник()
	
	Возврат "iss.moex.com";
	
КонецФункции

Функция АтрибутыУзлаВСтруктуру(УзелXML)
	
	Ответ = Неопределено;
	
	Если УзелXML.КоличествоАтрибутов() > 0 Тогда
		
		Ответ = Новый Структура();
		
		Для Сч = 0 По УзелXML.КоличествоАтрибутов()-1 Цикл 
			Ответ.Вставить(УзелXML.ИмяАтрибута(Сч), УзелXML.ЗначениеАтрибута(Сч));
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьКаталогВременныхФайловДляЗагрузки()
	
	Возврат КаталогВременныхФайлов() + "moex";
	
КонецФункции

#КонецОбласти

#Область СозданиеЭлементовВБД

Функция СоздатьОбновитьСправочникТорговыхСистем(СписокТорговыхСистем) Экспорт
	
	Для каждого СтрокаСписка из СписокТорговыхСистем цикл
		ТорговаяСистема = Справочники.СписокТорговыхСистем.НайтиПоРеквизиту("Заголовок", СтрокаСписка.Значение);
		Если ТорговаяСистема = Справочники.СписокТорговыхСистем.ПустаяСсылка() тогда
			НоваяТорговаяСистема = Справочники.СписокТорговыхСистем.СоздатьЭлемент();
			НоваяТорговаяСистема.Наименование 	= СтрокаСписка.Представление;
			НоваяТорговаяСистема.Заголовок 		= СтрокаСписка.Значение;
			НоваяТорговаяСистема.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьОбновитьСправочникРынков(СтрокаСпискаТорговыхСистем,СписокРынков) Экспорт
	
	Владелец = Справочники.СписокТорговыхСистем.НайтиПоРеквизиту("Заголовок",СтрокаСпискаТорговыхСистем.Значение);
	
	Для каждого СтрокаСписка из СписокРынков цикл
		Рынок = Справочники.Рынок.НайтиПоРеквизиту("Заголовок", СтрокаСписка.Значение,,Владелец);
		Если Рынок = Справочники.Рынок.ПустаяСсылка() тогда
			НовыйРынок = Справочники.Рынок.СоздатьЭлемент();
			НовыйРынок.Владелец 	= Владелец;
			НовыйРынок.Наименование = СтрокаСписка.Представление;
			НовыйРынок.Заголовок 	= СтрокаСписка.Значение;
			НовыйРынок.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция СоздатьОбновитьСправочникРежимаТоргов(СтрокаСпискаРынков, РежимыТоргов) Экспорт
	
	Владелец = Справочники.Рынок.НайтиПоРеквизиту("Заголовок",СтрокаСпискаРынков.Значение);
	
	Для каждого СтрокаСписка из РежимыТоргов цикл
		Режим = Справочники.РежимТоргов.НайтиПоРеквизиту("Заголовок", СтрокаСписка.Значение,,Владелец);
		Если Режим = Справочники.РежимТоргов.ПустаяСсылка() тогда
			НовыйРежим = Справочники.РежимТоргов.СоздатьЭлемент();
			НовыйРежим.Владелец 	= Владелец;
			НовыйРежим.Наименование = СтрокаСписка.Представление;
			НовыйРежим.Заголовок 	= СтрокаСписка.Значение;
			НовыйРежим.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ПолучениеСпискаССайта

Функция ПолучитьСписокТорговыхСистем(ВременныйКаталог)
	
	Попытка	
		
		Список = Новый СписокЗначений;		
		СоздатьКаталог(ВременныйКаталог);
		ИмяФайла = "engines.xml";
		ИмяВходящегоФайла = "" + ВременныйКаталог + "\" + ИмяФайла;
		
		СерверИсточник = ПолучитьСерверИсточник();
		СтрокаПараметраПолучения = "/iss/engines.xml";	
		
		HTTP = Новый HTTPСоединение(СерверИсточник);		
		HTTP.Получить(СтрокаПараметраПолучения, ИмяВходящегоФайла);		
		ВыбФайл = Новый Файл(ИмяВходящегоФайла);
		Если ВыбФайл.Существует() Тогда
			
			ЧтениеXML = Новый ЧтениеXML; 
			ЧтениеXML.ОткрытьФайл(ИмяВходящегоФайла);  
			
			//Открываем файл 
			Пока ЧтениеXML.Прочитать() Цикл  
				
				Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
					//Определяем начало элемента 
					Если ЧтениеXML.Имя="row" тогда						
						АтрибутыЭлемента = АтрибутыУзлаВСтруктуру(ЧтениеXML);						
						Если НЕ АтрибутыЭлемента=Неопределено Тогда
							Список.Добавить(АтрибутыЭлемента.name , АтрибутыЭлемента.title);
						КонецЕсли;						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ЧтениеXML.Закрыть();
			
		Иначе
			Возврат Неопределено;
		КонецЕсли;
				
		Возврат Список;
		
	Исключение
		Возврат Неопределено;
	КонецПопытки;	
		
КонецФункции

Функция ПолучитьСписокРынков(ВременныйКаталог,СтрокаСпискаТорговыхСистем)
	
	Попытка	
		Список = Новый СписокЗначений;
		
		ИмяФайла = "markets.xml";
		ИмяВходящегоФайла = "" + ВременныйКаталог + "\" + ИмяФайла;
		
		СерверИсточник="iss.moex.com";
		СтрокаПараметраПолучения = "/iss/engines/"+СтрокаСпискаТорговыхСистем.Значение + "/markets.xml";
		
		HTTP = Новый HTTPСоединение(СерверИсточник);
		HTTP.Получить(СтрокаПараметраПолучения, ИмяВходящегоФайла);
		
		ВыбФайл = Новый Файл(ИмяВходящегоФайла);
		Если ВыбФайл.Существует() Тогда
			
			ЧтениеXML = Новый ЧтениеXML; 
			ЧтениеXML.ОткрытьФайл(ИмяВходящегоФайла);  
			
			//Открываем файл 
			Пока ЧтениеXML.Прочитать() Цикл  
				
				Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
					//Определяем начало элемента 
					Если ЧтениеXML.Имя="row" тогда
						
						АтрибутыЭлемента = АтрибутыУзлаВСтруктуру(ЧтениеXML);
						
						Если НЕ АтрибутыЭлемента=Неопределено Тогда
							Список.Добавить(АтрибутыЭлемента.name, АтрибутыЭлемента.title);
						КонецЕсли;
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ЧтениеXML.Закрыть();
			
			Возврат Список;
			
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	Исключение
		Возврат Неопределено;
	КонецПопытки;	
	
КонецФункции

Функция ПолучитьРежимаТоргов(ВременныйКаталог,СтрокаСпискаТорговыхСистем, СтрокаСпискаРынков)
	
	Попытка	
		Список = Новый СписокЗначений;
		
		ИмяФайла = "boards.xml";
		ИмяВходящегоФайла = "" + ВременныйКаталог + "\" + ИмяФайла;
		
		СерверИсточник="iss.moex.com";
		СтрокаПараметраПолучения = "/iss/engines/"+СтрокаСпискаТорговыхСистем.Значение + "/markets/" + СтрокаСпискаРынков.Значение + "/boards.xml";
		
		HTTP = Новый HTTPСоединение(СерверИсточник);
		HTTP.Получить(СтрокаПараметраПолучения, ИмяВходящегоФайла);
		
		ВыбФайл = Новый Файл(ИмяВходящегоФайла);
		Если ВыбФайл.Существует() Тогда
			
			ЧтениеXML = Новый ЧтениеXML; 
			ЧтениеXML.ОткрытьФайл(ИмяВходящегоФайла);  
			
			//Открываем файл 
			Пока ЧтениеXML.Прочитать() Цикл  
				
				Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
					//Определяем начало элемента 
					Если ЧтениеXML.Имя="row" тогда
						
						АтрибутыЭлемента = АтрибутыУзлаВСтруктуру(ЧтениеXML);
						
						Если НЕ АтрибутыЭлемента=Неопределено Тогда
							Список.Добавить(АтрибутыЭлемента.boardid, АтрибутыЭлемента.title);
						КонецЕсли;					
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ЧтениеXML.Закрыть();
			
			Возврат Список;
			
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	Исключение
		Возврат Неопределено 	
	КонецПопытки;
	
КонецФункции

#КонецОбласти

Процедура ЗагрузитьОбновитьСтруктуруБлоковБиржи() Экспорт 
	
	ВременныйКаталог = ПолучитьКаталогВременныхФайловДляЗагрузки();
	
	СписокТорговыхСистем = ПолучитьСписокТорговыхСистем(ВременныйКаталог);
	Если СписокТорговыхСистем = Неопределено тогда
		Возврат;
	КонецЕсли;	
	
	СправочникТорговыхСистемОбновлен = ЗагрузкаСМосковскойБиржиСервер.СоздатьОбновитьСправочникТорговыхСистем(СписокТорговыхСистем);
	
	Если СправочникТорговыхСистемОбновлен тогда
		
		Для каждого СтрокаСпискаТорговыхСистем из СписокТорговыхСистем цикл
			
			СписокРынков = ПолучитьСписокРынков(ВременныйКаталог, СтрокаСпискаТорговыхСистем);	
			Если СписокРынков = Неопределено тогда
				Продолжить;
			КонецЕсли;	
			
			СправочникРынковОбновлен = ЗагрузкаСМосковскойБиржиСервер.СоздатьОбновитьСправочникРынков(СтрокаСпискаТорговыхСистем, СписокРынков);
			
			Если СправочникРынковОбновлен тогда
				
				Для каждого СтрокаСпискаРынков из СписокРынков цикл
					
					СписокРежимаТоргов = ПолучитьРежимаТоргов(ВременныйКаталог, СтрокаСпискаТорговыхСистем, СтрокаСпискаРынков);	
					
					Если СписокРежимаТоргов = Неопределено тогда
						Продолжить;
					КонецЕсли;
					
					ЗагрузкаСМосковскойБиржиСервер.СоздатьОбновитьСправочникРежимаТоргов(СтрокаСпискаРынков, СписокРежимаТоргов);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	УдалитьФайлы(ПолучитьКаталогВременныхФайловДляЗагрузки(),"*.*");
		
КонецПроцедуры

#Область ОбновлениеДанныхПоАктивам

Процедура ПолучитьИЗаписатьЦенуАктиваНаДату(Актив,Период) Экспорт
	
	Если Актив.Вид = Перечисления.ВидАктива.Акция тогда
		ТорговаяСистема = Справочники.СписокТорговыхСистем.НайтиПоНаименованию("Фондовый рынок и рынок депозитов").Заголовок;
		Рынок 			= Справочники.Рынок.НайтиПоНаименованию("Рынок акций").Заголовок;
		РежимТоргов 	= Справочники.РежимТоргов.НайтиПоНаименованию("Т+: Акции и ДР - безадрес.").Заголовок;
	ИначеЕсли Актив.Вид = Перечисления.ВидАктива.Облигация тогда
		ТорговаяСистема = "";
		Рынок 			= "";
		РежимТоргов 	= "";	
	ИначеЕсли Актив.Вид = Перечисления.ВидАктива.ETF тогда
		ТорговаяСистема = "";
		Рынок 			= "";
		РежимТоргов 	= "";		
	Иначе
		Возврат;
	КонецЕсли;
	
	Инструмент = Актив.Тикер;
	Периодичность = 24; // 1-минута; 10-10 минут; 60-час; 24-сутки
	
	НачалоПериода = НачалоДня(Период);
	КонецПериода  = КонецДня(Период);
	ТаблицаКурсов = Новый ТаблицаЗначений;
	
	ЧислоЦиклов = 0;
	Пока ТаблицаКурсов.Количество() = 0 Цикл
		ЧислоЦиклов = ЧислоЦиклов + 1;
		НачалоПериода = НачалоПериода - 60 * 60 * 24;
		КонецПериода  = КонецПериода - 60 * 60 * 24; 
		ТаблицаКурсов = ПолучитьТаблицуКотировок(ТорговаяСистема,Рынок,РежимТоргов,Инструмент,НачалоПериода,КонецПериода,Периодичность);
		Если ЧислоЦиклов = 14 тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаКурсов.Количество() = 0 тогда
		Возврат;		
	Иначе
		ПоследняяСтрока = ТаблицаКурсов[ТаблицаКурсов.Количество() - 1];
		ДатаЗакрытия = Дата(Сред(ПоследняяСтрока.end,9,2) + "." + Сред(ПоследняяСтрока.end,6,2) + "." + Лев(ПоследняяСтрока.end,4) + " " + Прав(ПоследняяСтрока.end,8));
		ЦенаЗакрытия = Число(ПоследняяСтрока.close);
	КонецЕсли;
	
	ОбщегоНазначенияСервер.ЗаписатьТекущуюЦенуАктива(Актив,ДатаЗакрытия,ЦенаЗакрытия);
	
КонецПроцедуры

Функция ПолучитьТаблицуКотировок(ТорговаяСистема,Рынок,РежимТоргов,Инструмент,НачалоПериода,КонецПериода,Периодичность)
	
	ТаблицаКурсов = Новый ТаблицаЗначений;
	
	ВремКаталог = КаталогВременныхФайлов() + "tempKurs";
	СоздатьКаталог(ВремКаталог);
	УдалитьФайлы(ВремКаталог,"*.*");
	ИмяФайла = "candles.xml";
	ИмяВходящегоФайла = "" + ВремКаталог + "\" + ИмяФайла;
	
	СерверИсточник = ПолучитьСерверИсточник();
	СтрокаПараметраПолучения = "iss/engines/" + ТорговаяСистема + "/markets/" + Рынок + "/boards/" + РежимТоргов + "/securities/" + Инструмент + "/candles.xml";
	СтрокаПараметраПолучения = СтрокаПараметраПолучения + "?from=" + Формат(НачалоПериода, "ДФ=yyyy-MM-dd");
	СтрокаПараметраПолучения = СтрокаПараметраПолучения + "&till=" + Формат(КонецПериода, "ДФ=yyyy-MM-dd");
	СтрокаПараметраПолучения = СтрокаПараметраПолучения + "&interval=" + Периодичность;
	
	Попытка
		HTTP = Новый HTTPСоединение(СерверИсточник);
		HTTP.Получить(СтрокаПараметраПолучения, ИмяВходящегоФайла);
		
		ВыбФайл = Новый Файл(ИмяВходящегоФайла);
		Если ВыбФайл.Существует() Тогда
			
			ЧтениеXML = Новый ЧтениеXML; 
			ЧтениеXML.ОткрытьФайл(ИмяВходящегоФайла);  
			
			МассивОткрытыхСтрок = Новый Массив; // тут будем хранить все имена открытых срок
			//Открываем файл 
			Пока ЧтениеXML.Прочитать() Цикл  
				
				Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда  
					//Определяем начало элемента 
					МассивОткрытыхСтрок.Добавить(ЧтениеXML.Имя);
					Если ЧтениеXML.Имя = "data" тогда
						АтрибутыЭлемента = АтрибутыУзлаВСтруктуру(ЧтениеXML);
						Если НЕ АтрибутыЭлемента = Неопределено Тогда
							ИмяДата = Неопределено;
							Если АтрибутыЭлемента.Свойство("id", ИмяДата) Тогда
								Если ИмяДата = "candles" Тогда
									МассивОткрытыхСтрок[МассивОткрытыхСтрок.Количество()-1] = "candles";
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Если ЧтениеXML.Имя = "column" тогда
						Если МассивОткрытыхСтрок[1] = "candles" Тогда
							АтрибутыЭлемента = АтрибутыУзлаВСтруктуру(ЧтениеXML);
							Если НЕ АтрибутыЭлемента = Неопределено Тогда
								ТаблицаКурсов.Колонки.Добавить(АтрибутыЭлемента.NAME,,,);
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Если ЧтениеXML.Имя = "row" тогда 
						Если МассивОткрытыхСтрок[1] = "candles" Тогда
							АтрибутыЭлемента = АтрибутыУзлаВСтруктуру(ЧтениеXML);
							Если НЕ АтрибутыЭлемента = Неопределено Тогда
								Строка = ТаблицаКурсов.Добавить();
								ЗаполнитьЗначенияСвойств(Строка,АтрибутыЭлемента); 
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда  
					
					МассивОткрытыхСтрок.Удалить(МассивОткрытыхСтрок.Количество()-1);	
					
				КонецЕсли;
				
			КонецЦикла;
			
			ЧтениеXML.Закрыть();
			
		Иначе
			//Предупреждение("Файл не найден!");
		КонецЕсли;
		
		УдалитьФайлы(ВремКаталог,"*.*");
	Исключение
		
	КонецПопытки;
	
	Возврат ТаблицаКурсов;
	
КонецФункции



#КонецОбласти